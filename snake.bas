
100 REM SPEED LIMIT
110 SP = 1000

200 REM SET RANDOM SEED
210 LET SEED = PEEK(78) + PEEK(79) * 256
220 LET TMP = RND(-SEED)

300 REM CURSOR KEY DIRECTIONS
310 UP = 139: DN = 138: LF = 136: RT = 149

400 TEXT: HOME:   REM RESET SCREEN
410 LET W = 40:   REM SCREEN WIDTH 
420 LET H = 24:   REM SCREEN HEIGHT
430 LET X = 0:    REM ARRAY X INDEX
440 LET Y = 1:    REM ARRAY Y INDEX
450 LET L = 1:    REM SNAKE LENGTH
460 DIM P(200,2): REM SNAKE POSITION ARRAY

500 REM SETUP SNAKE AND FOOD 
510 LET K = DN
520 LET FX = INT(RND(1) * W) + 1
530 LET FY = INT(RND(1) * H) + 1
540 P(0,X) = INT(W/2): P(0,Y) = INT(H/2)

1000 REM *** GAME LOOP ***
1010 GOSUB 2000: REM MOVE ARRAY
1020 GOSUB 3000: REM MOVE SNAKE
1030 GOSUB 4000: REM TEST
1040 GOSUB 5000: REM DRAW
1050 FOR I = 1 TO SP: NEXT I: REM PAUSE
1499 GOTO 1000

2000 REM *** MOVE ARRAY VALUES ***
2010 FOR I=L TO 1 STEP -1
2020 P(I,X) = P(I-1,X): P(I,Y) = P(I-1,Y)
2030 NEXT I
2499 RETURN

3000 REM *** MOVE SNAKE ***
3010 LET Q = PEEK(49152):
3020 IF Q = LF OR Q = RT OR Q = UP OR Q = DN THEN K = Q
3030 IF K = LF THEN P(0,X) = P(0,X) - 1
3040 IF K = RT THEN P(0,X) = P(0,X) + 1 
3050 IF K = UP THEN P(0,Y) = P(0,Y) - 1
3060 IF K = DN THEN P(0,Y) = P(0,Y) + 1
3499 RETURN

4000 REM *** TESTS ***
4010 REM OUT OF BOUNDS
4020 IF P(0,X) > W OR P(0,Y) > H GOTO 7000
4030 IF P(0,X) < 1 OR P(0,Y) < 1 GOTO 7000
4040 REM SNAKE ATE SNAKE
4050 FOR I=1 TO L
4060 IF P(0,X) = P(I,X) AND P(0,Y) = P(I,Y) GOTO 7000
4070 NEXT I 
4080 REM SNAKE ATE FOOD
4090 IF P(0,X) = FX AND P(0,Y) = FY THEN GOSUB 6000
4499 RETURN

5000 REM *** DRAW ***
5010 HTAB P(L,X): VTAB P(L,Y): PRINT " ";
5030 FOR I = 0 TO L-1
5040 HTAB P(I,X): VTAB P(I,Y): PRINT CHR$(127);
5060 NEXT I
5070 HTAB FX: VTAB FY: PRINT CHR$(64);
5499 RETURN

6000 REM *** GROW THE SNAKE AND ADD NEW FOOD ***
6010 L = L + 1
6020 P(L,X) = P(L-1,X): P(L,Y) = P(L-1,Y)
6030 IF SP > 20 THEN SP = SP - 20
6040 LET FX = INT(RND(1) * W) + 1
6050 LET FY = INT(RND(1) * H) + 1
6499 RETURN

7000 REM *** GAME OVER ***
7010 TEXT: HOME
7020 HTAB 8: VTAB 12: PRINT "GAME OVER MAN, GAME OVER!";
7030 END

